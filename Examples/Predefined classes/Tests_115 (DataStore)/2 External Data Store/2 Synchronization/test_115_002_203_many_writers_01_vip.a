-----------------------------------------------------------------------
-- An example of Actor Prolog program.                               --
-- (c) 2014 IRE RAS Alexei A. Morozov                                --
-----------------------------------------------------------------------
--
-- Warning: the VIP version of the program is unstable.
-- Please translate this example to Java.
--
-----------------------------------------------------------------------
class 'Main' (specialized 'Alpha'):
--
constant:
--
	number_of_agents	= 100;
--
	agents	= 'AgentArray'[number_of_agents] of
			(('MyAgent',
				data_store=store));
--
internal:
--
	store	= ('DataStore',
			name="ManyWritersDataStore.dat",
			access_mode='modifying',
			sharing_mode='shared_access');
--
	con	= ('Console');
--
[
goal:-
	-- store ? delete,
	con ? set_color('Orange'),
	con ? write("OPENING DATA STORE... "),
	store ? open,
	con ? writeln("DONE."),
	con ? set_color('default'),
	fail.
goal:-
	[agents[N]] << initiate(N,number_of_agents),
		fail.
]
-----------------------------------------------------------------------
class 'AgentArray' (specialized 'DynamicWorlds'):
[
]
-----------------------------------------------------------------------
class 'MyAgent' (specialized 'Timer'):
--
constant:
--
	data_store;
--
internal:
--
	database	= ('MyDatabase',
				place=shared(data_store,"MyTable"),
				con);
--
	con		= ('Report',
				width=target_width,
				height=target_height);
--
constant:
--
	target_width	= 25;
	target_height	= 7;
--
[
goal:-!.
--
initiate(N,Max):-
	X1== ?random(80-target_width+1),
	Y1== ?random(25-target_height+1),
	con ? set_x(X1),
	con ? set_y(Y1),
	R1== (128+?random(128))/255,
	G1== (128+?random(128))/255,
	B1== (128+?random(128))/255,
	con ? set_background_color(color3(R1,G1,B1)),
	con ? set_title(con?format(" %d / %d",N,Max)),
	con ? show,
	set_period(0.0,0.0),
	activate.
--
tick:-!,
	database ? test_database.
]
-----------------------------------------------------------------------
class 'MyDatabase' (specialized 'Database'):
--
internal:
--
	con;
--
[
test_database:-
	con ? set_color('Black'),
	con ? writeln("Test database update."),
	con ? set_color('default'),
	begin_transaction('modifying'),!,
	report_whether_database_is_updated,
	update_database,
	end_transaction.
test_database:-
	con ? set_color('White','Violet'),
	con ? writeln("Time-out!\nI cannot open transaction."),
	con ? set_color('default','default').
--
report_whether_database_is_updated:-
	is_updated,!,
	con ? set_color('White','DkGreen'),
	con ? writeln("The database is updated!"),
	con ? set_color('default','default').
report_whether_database_is_updated:-
	con ? set_color('Navy','default'),
	con ? writeln("The database is NOT updated!"),
	con ? set_color('default','default').
--
update_database:-
	T1== ?match(_),
	T1 == my_data(V1),
	report_checksum(V1),
	fail.
update_database:-
	Delta== ?random(101) - 50.0,
	update_database(Delta),
	update_database(-(Delta)).
--
update_database(Delta):-
	T1== ?match(_),
	T1 == my_data(V1),
	retract(T1),!,
	insert(my_data(V1 + Delta)).
update_database(Delta):-
	insert(my_data(Delta)).
--
report_checksum(V1):-
	V1 <> 0.0,
	con ? set_color('White','Red'),
	con ? writeln("Illegal check sum value!"),
	con ? set_color('default','default'),
	fail.
report_checksum(V1):-
	con ? writeln("Check sum: ",V1).
]
-----------------------------------------------------------------------
