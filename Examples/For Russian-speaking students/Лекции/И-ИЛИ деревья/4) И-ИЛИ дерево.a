----------------------------------------------------
--                                                --
-- Что такое И-дерево (дерево доказательства) и   --
-- ИЛИ-дерево (дерево поиска)?                    --
--                                                --
-- (4) Кто чей дедушка?                           --
--                                                --
----------------------------------------------------

CLAUSES:

отец("Александр","Алексей").
отец("Михаил","Александр").
отец("Борис","Людмила").

мать("Нина","Александр").
мать("Людмила","Алексей").
мать("Зоя","Людмила").

дедушка(X,Y):-
        отец(ИмяОтца,Y),
        отец(X,ИмяОтца).
дедушка(X,Y):-
        мать(ИмяМатери,Y),
        отец(X,ИмяМатери).

goal:-
        дедушка(X,"Алексей"),
        writeln(X," дедушка Алексея."),
        fail.

/*
Этой программе соответствуют
два И-дерева (дерева доказательства).

Первое И-дерево:

               целевое утверждение
                  /     |     \
                 /      |      \
             дедушка  writeln  fail
              /   \
            отец  отец

Второе И-дерево:

               целевое утверждение
                  /     |     \
                 /      |      \
             дедушка  writeln  fail
              /   \
            мать  отец

(Аргументы подцелей не указаны для упрощения.)

Дерево поиска (ИЛИ-дерево):

                Подцели: дедушка(X,"Алексей"), writeln(...), fail.
                (целевое утверждение программы)
                            / \
дедушка(X,Y):-             /   \  дедушка(X,Y):-
        отец(ИмяОтца,Y),  /     \         мать(ИмяМатери,Y),
        отец(X,ИмяОтца). /       \        отец(X,ИмяМатери).
                        /         \
                       /           \
                      /             \
                     /               \
                    /                 \
                   /                   \
                  /                     \
                 /                       \
                /                         \
               /                           \
Подцели:                             Подцели:
отец(ИмяОтца,"Алексей"),             мать(ИмяМатери,"Алексей"),
отец(X,ИмяОтца),                     отец(X,ИмяМатери),
writeln(X,...),                      writeln(X,...),
fail.                                fail.
|                                    |
| отец("Александр","Алексей").       | мать("Людмила","Алексей").
|                                    |
Подцели:                             Подцели:
отец(X,"Александр"),                 отец(X,"Людмила"),
writeln(X,...),                      writeln(X,...),
fail.                                fail.
|                                    |
| отец("Михаил","Александр").        | отец("Борис","Людмила").
|                                    |
Подцели:                             Подцели:
writeln(                             writeln(
     "Михаил",                            "Борис",
     " дедушка Алексея."),                " дедушка Алексея."),
fail.                                fail.
|                                    |
| Вывод на экран.                    | Вывод на экран.
|                                    |
Подцели:                             Подцели:
fail.                                fail.
|                                    |
Данная ветвь исполнения              Вторая ветвь исполнения
программы завершилась                программы тоже завершилось
неудачей.                            неудачей.

Результаты работы программы:

Михаил дедушка Алексея.
Борис дедушка Алексея.

*/
