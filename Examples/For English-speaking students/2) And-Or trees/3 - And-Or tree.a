-------------------------------------------
--                                       --
--         Actor Prolog lessons          --
--      (c) 2015 Alexei A. Morozov       --
--                                       --
-- The topic: And-Or trees. A cut.       --
--                                       --
-- Questions: What is the And-Or tree?   --
-- What is a search tree (the Or tree)   --
-- of a logic program? What is a proof   --
-- tree (the And tree) of the logic      --
-- program? What is a cut? How the cut   --
-- changes the execution of a            --
-- non-deterministic logic program?      --
--                                       --
-------------------------------------------

CLAUSES:

goal:-
	a,
	fail.

a:-
	b,
	c,
	d.

b:-
	writeln("B").

c:-
	e,!,
	f.
c:-
	g.

e:-
	writeln("E").

f:-
	writeln("F").

g:-
	writeln("G").

d:-
	writeln("D").

/*
This program corresponds to several
(two) And trees (proof trees).

Here is the first And tree (it looks like
the tree in the previous example):

                 the goal
                 statement
                 /    \
                a      fail
              / | \
             b  c  d
               /|\
              / | \
             e  !  f

This is the second And tree:

                 the goal
                 statement
                 /    \
                a      fail
              / | \
             b  c  d
                |
                g

(The b, d, e, f, and g sub-goals are not
detailed for the sake of simplicity.)

Here is the Or tree (the search tree) of
the program:

                Sub-goals: a, fail.
                (the goal statement
                of the program)
                |
                | a :- b, c, d.
                |
                Sub-goals: b, c, d, fail.
                |
                | b.
                |
                Sub-goals: c, d, fail.
               / \
c :- e, !, f. /   \ c :- g.
             /     \
            /       \
           /         \   The second branch of
          /           \  the Or tree will be
         /             \ cut.
        /               \
       /                 \
      /                   \
     /                     \
Sub-goals: e,!,f,d,fail.    Sub-goals: g, d, fail.
|                           |
| e.                        | g.
|                           |
Sub-goals: !, f, d, fail.   Sub-goals: d, fail.
|                           |
| Forgot the second branch  | d.
| of the Or tree!           |
|                           Sub-goals: fail.
Sub-goals: f, d, fail.      |
|                           This branch of the Or tree
| f.                        of the program is terminated
|                           with a failure too.
Sub-goals: d, fail.
|
| d.
|
Sub-goals: fail.
|
This branch of the Or tree
of the program is terminated
with a failure.

*/
