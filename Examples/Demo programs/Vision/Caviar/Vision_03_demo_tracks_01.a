-----------------------------------------------------------------------
-- An example of Actor Prolog program.                               --
-- (c) 2013 IRE RAS Alexei A. Morozov                                --
-----------------------------------------------------------------------
import .. from "morozov/Java2D";
import .. from "morozov/Vision";
-----------------------------------------------------------------------
class 'Main' (specialized 'Timer'):
--
constant:
--
	data_directory		= "data";
	target_directory	= "Fight_RunAway1";
	sampling_rate		= 25;
	circle_radius		= 0.01;
--
internal:
--
	subtractor	= ('ImageSubtractor',
				extract_blobs= 'yes',
				track_blobs= 'yes',
				use_grayscale_colors= 'yes',
				make_rectangular_blobs_in_synthesized_image= 'no');
	graphic_window	= ('Canvas2D',
				background_color= 'SystemControl',
				y= 0,
				height= 22);
	prompt_window	= ('Report',
				y= 22,
				height= 3);
	text		= ('Text');
	image		= ('BufferedImage');
	state		= ('ProgramState');
--
[
goal:-!,
	graphic_window ? show,
	prompt_window ? write(
		"The data are coming from the EC Funded CAVIAR ",
		"project / IST 2001 37540,\n",
		"found at URL: ",
		"http://homepages.inf.ed.ac.uk/rbf/CAVIAR/."),
	Time0== ?milliseconds(),
	state ? set_beginning_time(Time0),
	set_period((1/sampling_rate/2),0),
	activate.
--
tick:-
	T2== ?milliseconds(),
	state ? get_beginning_time(T1),
	Delta== (T2 - T1) / 1000.0 * sampling_rate,
	N== ?convert_to_integer(?round(Delta)),!,
	load_figure(N,T2).
--
load_figure(N2,_):-
	state ? get_current_frame(N1),
	N1 == N2,!.
load_figure(N,_):-
	state ? set_current_frame(N),
	ImageToBeLoaded==
		"jar:" + data_directory + "/" +
		target_directory + "_jpg" + "/JPEGS/" +
		target_directory +
		text?format("%03d",N) + ".jpg",
	image ? does_exist(ImageToBeLoaded),!,
	image ? load(ImageToBeLoaded),
	subtractor ? subtract(N,image),
	draw_scene.
load_figure(_,T2):-
	state ? set_beginning_time(T2),
	subtractor ? reset_results.
--
draw_scene:-
	subtractor ? commit,
	graphic_window ? suspend_redrawing,
	graphic_window ? clear,
	subtractor ? get_background_image(image),
	graphic_window ? draw_image(image,0,0,1,1),
	subtractor ? get_synthesized_image(image),
	graphic_window ? draw_image(image,0,0,1,1),
	subtractor ? get_tracks(Tracks),
	image ? get_size_in_pixels(IW,IH),
	draw_tracks(IW,IH,Tracks),
	graphic_window ? draw_now.
--
draw_tracks(IW,IH,[Track|Rest]):-!,
	draw_track(IW,IH,Track),
	draw_tracks(IW,IH,Rest).
draw_tracks(_,_,_).
--
draw_track(IW,IH,{identifier:Id,segments:Segments|_}):-
	graphic_window ? set_brush('Green'),
	graphic_window ? set_font({size:18}),
	graphic_window ? set_text_alignment('CENTER','CENTER'),
	draw_track_segments(IW,IH,Id,Segments),
	fail.
draw_track(_,_,_).
--
draw_track_segments(IW,IH,Id,[Segment|Rest]):-!,
	draw_track_segment(IW,IH,Id,Segment),
	draw_track_segments(IW,IH,Id,Rest).
draw_track_segments(_,_,_,_).
--
draw_track_segment(
		IW,IH,Id,{
			x1:X1a,y1:Y1a,
			x2:X2a,y2:Y2a,
			coordinates:TrackOfBlobs|_}):-
	X1r== X1a / IW,
	Y1r== Y1a / IH,
	X2r== X2a / IW,
	Y2r== Y2a / IH,
	graphic_window ? set_pen({color:'Magenta',lineWidth:2}),
	draw_track_of_blobs(IW,IH,TrackOfBlobs),
	graphic_window ? set_pen({color:'Green',lineWidth:1}),
	graphic_window ? draw_line(X1r,Y1r,X2r,Y2r),
	draw_circle(X1r,Y1r),
	draw_circle(X2r,Y2r),
	draw_blob_identifier(X1r,Y1r,X2r,Y2r,Id),
	fail.
draw_track_segment(_,_,_,_).
--
draw_track_of_blobs(IW,IH,[Blob|Rest]):-
	Blob== {x:Xa,y:Ya|_},!,
	Xr== Xa / IW,
	Yr== Ya / IH,
	draw_track_of_blobs(IW,IH,Xr,Yr,Rest).
draw_track_of_blobs(_,_,_).
--
draw_track_of_blobs(IW,IH,X1,Y1,[Blob|Rest]):-
	Blob== {x:Xa,y:Ya|_},!,
	X2== Xa / IW,
	Y2== Ya / IH,
	graphic_window ? draw_line(X1,Y1,X2,Y2),
	draw_track_of_blobs(IW,IH,X2,Y2,Rest).
draw_track_of_blobs(_,_,_,_,_).
--
draw_circle(X0,Y0):-
	X1== X0 - circle_radius,
	Y1== Y0 - circle_radius,
	Width== circle_radius * 2,
	Height== circle_radius * 2,
	graphic_window ? draw_ellipse(X1,Y1,Width,Height).
--
draw_blob_identifier(X1,Y1,X2,Y2,Id):-!,
	Text== text?format("%d",Id),
	graphic_window ? draw_text((X1+X2)/2,(Y1+Y2)/2,Text).
]
-----------------------------------------------------------------------
class 'ProgramState' (specialized 'Database'):
[
CLAUSES:
--
get_beginning_time(T):-
	Item== ?match(beginning_time(_)),
	Item == beginning_time(T),!.
--
set_beginning_time(T):-
	retract_all(beginning_time(_)),
	append(beginning_time(T)).
--
get_current_frame(N):-
	Item== ?match(current_frame(_)),
	Item == current_frame(N),!.
--
set_current_frame(N):-
	retract_all(current_frame(_)),
	append(current_frame(N)).
]
-----------------------------------------------------------------------
