-----------------------------------------------------------------------
-- An example of Actor Prolog program.                               --
-- (c) 2013 IRE RAS Alexei A. Morozov                                --
-----------------------------------------------------------------------
import .. from "morozov/Java2D";
-----------------------------------------------------------------------
--
DOMAINS:
--
ObjectType	= 'POINT'; 'RECTANGLE'; 'ROUNDRECTANGLE2D';
		  'ELLIPSE2D'; 'ARC2D';
		  'LINE2D'; 'QUADCURVE2D'; 'CUBICCURVE2D';
		  'POLYGON'.
--
-----------------------------------------------------------------------
class 'Main' (specialized 'Toolbox2D'):
--
constant:
--
	title		= "Drawing Geometric Shapes";
--
	background_color		= 'White';
	enable_scene_antialiasing	= 'yes';
--
	scaling_factor			= 'MIN';
--
	round_rectangle_arc_width	= 0.1;
	round_rectangle_arc_height	= 0.1;
	arc_starting_angle		= 30;
	arc_angular_extent		= 120;
--
	menu	= [
		submenu("&File",[
			item("Sa&ve","pressed F2")
			]),
		submenu("&Draw",[
			item("Po&int","alt pressed I"),
			item("Rec&tangle","alt pressed T"),
			item("R&oundRectangle","alt pressed O"),
			item("Ellip&se","alt pressed S"),
			item("&Arc","alt pressed A"),
			item("&Line","alt pressed L"),
			item("&QuadCurve","alt pressed Q"),
			item("Cu&bicCurve","alt pressed B"),
			item("&Polygon","alt pressed P")
			]),
		submenu("&Edit",[
			item("&Undo","alt control pressed Z"),
			item("&Redo","shift control pressed Z"),
			item("&Clear","alt pressed C")
			])
		];
--
internal:
--
	mode;
--
[
PREDICATES:
--
imperative:
--
draw_object_preview(
	ObjectType,
	PointX,
	PointY)			- (i,i,i);
--
determ:
--
draw_object_or_Fail(
	ObjectType,
	PointX,
	PointY,
	PointX,
	PointY,
	MouseEvent,
	DrawingMarker)		- (i,i,i,i,i,i,i);
--
imperative:
--
append_polygon_point(PointX,PointY)		- (i,i);
--
drag_object(
	ObjectType,
	PointX,
	PointY,
	PointX,
	PointY)			- (i,i,i,i,i);
--
undo_operations;
redo_operations;
--
CLAUSES:
--
goal:-!,
	enable_mouse_events(
		'MOUSE_PRESSED',
		'MOUSE_RELEASED',
		'MOUSE_DRAGGED'),
	initiate_graphics,
	Marker== ?mark_drawing(),
	mode ? remember_drawing_marker(Marker).
--
action("Save"):-!,
	save_image.
action("Undo"):-!,
	undo_operations.
action("Redo"):-!,
	redo_operations.
action("Clear"):-!,
	initiate_graphics,
	Marker== ?mark_drawing(),
	mode ? remember_drawing_marker(Marker).
action(BehaviorName):-!,
	mode ? set_object_type(BehaviorName).
--
mouse_pressed({x:X0,y:Y0|_}):-!,
	mode ? append_point(X0,Y0),
	mode ? get_object_type(Type),
	draw_object_preview(Type,X0,Y0).
--
mouse_released({x:X0,y:Y0|Rest}):-
	mode ? get_previous_point(X1,Y1),
	mode ? get_drawing_marker(Marker1),
	mode ? get_object_type(Type),
	draw_object_or_Fail(Type,X0,Y0,X1,Y1,Rest,Marker1),!,
	mode ? clear_point_list,
	Marker2== ?mark_drawing(),
	mode ? remember_drawing_marker(Marker2).
mouse_released(_):-!.
--
mouse_dragged({x:X0,y:Y0|_}):-
	mode ? get_previous_point(X1,Y1),!,
	mode ? get_object_type(Type),
	drag_object(Type,X0,Y0,X1,Y1).
mouse_dragged(_):-!.
--
draw_object_preview('POLYGON',X0,Y0):-
	mode ? get_previous_points(List1),!,
	reverse_points(List1,[p(X0,Y0)],List2),
	set_preliminary_pen,
	draw_lines_or_point(List2).
draw_object_preview(_,_,_).
--
draw_object_or_Fail('POINT',X0,Y0,_,_,_,Marker1):-!,
	retract_drawing(Marker1),
	draw_point(X0,Y0).
draw_object_or_Fail('RECTANGLE',X0,Y0,X1,Y1,_,Marker1):-!,
	retract_drawing(Marker1),
	draw_rectangle(X1,Y1,X0-X1,Y0-Y1).
draw_object_or_Fail('ROUNDRECTANGLE2D',X0,Y0,X1,Y1,_,Marker1):-!,
	retract_drawing(Marker1),
	draw_round_rectangle(
		X1,Y1,X0-X1,Y0-Y1,
		round_rectangle_arc_width,
		round_rectangle_arc_height).
draw_object_or_Fail('ELLIPSE2D',X0,Y0,X1,Y1,_,Marker1):-!,
	retract_drawing(Marker1),
	draw_ellipse(X1,Y1,X0-X1,Y0-Y1).
draw_object_or_Fail('ARC2D',X0,Y0,X1,Y1,_,Marker1):-!,
	retract_drawing(Marker1),
	draw_arc(
		X1,Y1,X0-X1,Y0-Y1,
		arc_starting_angle,
		arc_angular_extent,
		'OPEN').
draw_object_or_Fail('LINE2D',X0,Y0,X1,Y1,_,Marker1):-!,
	retract_drawing(Marker1),
	draw_line(X1,Y1,X0,Y0).
draw_object_or_Fail('QUADCURVE2D',X0,Y0,_,_,_,Marker1):-
	mode ? get_previous_points(X1,Y1,_,_,X3,Y3),!,
	retract_drawing(Marker1),
	draw_quad_curve(X3,Y3,X1,Y1,X0,Y0).
draw_object_or_Fail('QUADCURVE2D',X0,Y0,_,_,_,_):-!,
	mode ? append_point(X0,Y0),
	fail.
draw_object_or_Fail('CUBICCURVE2D',X0,Y0,_,_,_,Marker1):-
	mode ? get_previous_points(X1,Y1,_,_,X3,Y3,_,_,X5,Y5),!,
	retract_drawing(Marker1),
	draw_cubic_curve(X5,Y5,X3,Y3,X1,Y1,X0,Y0).
draw_object_or_Fail('CUBICCURVE2D',X0,Y0,_,_,_,_):-!,
	mode ? append_point(X0,Y0),
	fail.
draw_object_or_Fail('POLYGON',X0,Y0,_,_,MouseEvent,Marker1):-
	is_end_point(MouseEvent),
	append_polygon_point(X0,Y0),
	retract_drawing(Marker1),
	mode ? get_previous_points(List1),!,
	reverse_points(List1,[],List2),
	draw_polygon(List2).
draw_object_or_Fail('POLYGON',X0,Y0,_,_,_,Marker1):-
	append_polygon_point(X0,Y0),
	retract_drawing(Marker1),
	mode ? get_previous_points(List1),
	reverse_points(List1,[],List2),
	set_preliminary_pen,
	draw_lines_or_point(List2),
	fail.
--
append_polygon_point(X0,Y0):-
	mode ? get_previous_points(_,_,_,_),!,
	mode ? replace_recent_point(X0,Y0).
append_polygon_point(X0,Y0):-
	mode ? append_point(X0,Y0).
--
drag_object(_,_,_,_,_):-
	mode ? get_drawing_marker(Marker),
	retract_drawing(Marker),
	fail.
drag_object('POINT',X0,Y0,_,_):-!,
	set_preliminary_pen,
	draw_point(X0,Y0).
drag_object('RECTANGLE',X0,Y0,X1,Y1):-!,
	set_preliminary_pen,
	draw_rectangle(X1,Y1,X0-X1,Y0-Y1).
drag_object('ROUNDRECTANGLE2D',X0,Y0,X1,Y1):-!,
	draw_frame(X1,Y1,X0-X1,Y0-Y1),
	set_preliminary_pen,
	draw_round_rectangle(
		X1,Y1,X0-X1,Y0-Y1,
		round_rectangle_arc_width,
		round_rectangle_arc_height).
drag_object('ELLIPSE2D',X0,Y0,X1,Y1):-!,
	draw_frame(X1,Y1,X0-X1,Y0-Y1),
	set_preliminary_pen,
	draw_ellipse(X1,Y1,X0-X1,Y0-Y1).
drag_object('ARC2D',X0,Y0,X1,Y1):-!,
	draw_frame(X1,Y1,X0-X1,Y0-Y1),
	set_preliminary_pen,
	draw_arc(
		X1,Y1,X0-X1,Y0-Y1,
		arc_starting_angle,
		arc_angular_extent).
drag_object('LINE2D',X0,Y0,X1,Y1):-!,
	set_preliminary_pen,
	draw_line(X1,Y1,X0,Y0).
drag_object('POLYGON',X0,Y0,_,_):-
	mode ? get_previous_points(_,_,_,_),
	mode ? replace_recent_point(X0,Y0),
	mode ? get_previous_points(List1),!,
	reverse_points(List1,[p(X0,Y0)],List2),
	set_preliminary_pen,
	draw_lines_or_point(List2).
drag_object('POLYGON',X0,Y0,_,_):-
	mode ? get_previous_points(List1),!,
	reverse_points(List1,[p(X0,Y0)],List2),
	set_preliminary_pen,
	draw_lines_or_point(List2).
drag_object('POLYGON',_,_,_,_):-!.
drag_object('QUADCURVE2D',X0,Y0,_,_):-
	mode ? get_previous_points(X1,Y1,X2,Y2,X3,Y3),!,
	set_auxiliary_pen,
	draw_line(X3,Y3,X2,Y2),
	draw_small_circle(X1,Y1),
	set_preliminary_pen,
	draw_quad_curve(X3,Y3,X1,Y1,X0,Y0).
drag_object('QUADCURVE2D',X0,Y0,X1,Y1):-!,
	set_auxiliary_pen,
	draw_line(X1,Y1,X0,Y0).
drag_object('CUBICCURVE2D',X0,Y0,_,_):-
	mode ? get_previous_points(X1,Y1,X2,Y2,X3,Y3,X4,Y4,X5,Y5),!,
	set_auxiliary_pen,
	draw_line(X5,Y5,X4,Y4),
	draw_quad_curve(X5,Y5,X3,Y3,X2,Y2),
	draw_small_circle(X1,Y1),
	draw_small_circle(X3,Y3),
	set_preliminary_pen,
	draw_cubic_curve(X5,Y5,X3,Y3,X1,Y1,X0,Y0).
drag_object('CUBICCURVE2D',X0,Y0,_,_):-
	mode ? get_previous_points(X1,Y1,X2,Y2,X3,Y3),!,
	set_auxiliary_pen,
	draw_line(X3,Y3,X2,Y2),
	draw_small_circle(X1,Y1),
	set_preliminary_pen,
	draw_quad_curve(X3,Y3,X1,Y1,X0,Y0).
drag_object('CUBICCURVE2D',X0,Y0,X1,Y1):-
	set_auxiliary_pen,
	draw_line(X1,Y1,X0,Y0).
--
undo_operations:-
	Marker== ?mark_drawing(),
	Marker <= 2,!.
undo_operations:-
	mode ? get_drawing_marker(Marker1),
	retract_drawing(Marker1),
	fail.
undo_operations:-
	mode ? get_previous_point(_,_),!,
	mode ? retract_all(point_list(_)).
undo_operations:-
	mode ? reset_database,
	undo_drawing,
	Marker== ?mark_drawing(),
	mode ? remember_drawing_marker(Marker).
--
redo_operations:-
	mode ? get_previous_point(_,_),!.
redo_operations:-
	redo_drawing,
	Marker== ?mark_drawing(),
	mode ? remember_drawing_marker(Marker).
]
-----------------------------------------------------------------------
class 'Toolbox2D' (specialized 'Canvas2D'):
--
constant:
--
	small_rect_width	= 0.01;
	small_rect_height	= 0.01;
--
internal:
--
	mode			= ('DrawerDatabase');
--
	con			= ('Console');
--
[
PREDICATES:
--
determ:
--
is_end_point(MouseEvent)			- (i);
--
imperative:
--
reverse_points(
	PointList,PointList,PointList)		- (i,i,o);
--
draw_frame(PointX,PointY,PointX,PointY)		- (i,i,i,i);
draw_small_rectangle(PointX,PointY)		- (i,i);
--
draw_small_circle(PointX,PointY)		- (i,i);
--
draw_lines_or_point(PointList)			- (i);
draw_lines(PointList)				- (i);
--
initiate_graphics;
--
set_main_pen;
set_preliminary_pen;
set_auxiliary_pen;
--
save_image;
--
'+'(PointX,PointX) = PointX			- (i,i);
'+'(PointY,PointY) = PointY			- (i,i);
'-'(PointX,PointX) = PointX			- (i,i);
'-'(PointY,PointY) = PointY			- (i,i);
--
CLAUSES:
--
is_end_point({isShiftDown:'yes',clickCount:N|_}):-
	N > 1,!,
	fail.
is_end_point({isShiftDown:'yes'|_}):-!.
is_end_point({clickCount:N|_}):-
	N > 1,!.
--
reverse_points([Point|Rest1],List2,List3):-!,
	reverse_points(Rest1,[Point|List2],List3).
reverse_points([],List,List).
--
initiate_graphics:-
	clear,
	mode ? reset_database,
	set_rendering_mode({
		antialiasing:'on',
		rendering:'QUALITY',
		alphaInterpolation:'QUALITY',
		interpolation:'BICUBIC',
		strokeControl:'NORMALIZE'}),
	set_main_pen.
--
draw_frame(X1,Y1,Width,Height):-
	set_auxiliary_pen,
	draw_rectangle(X1,Y1,Width,Height),
	set_pen({
		color:'Black',
		lineWidth:1}),
	set_brush('Green'),
	draw_small_rectangle(X1,Y1),
	draw_small_rectangle(X1+Width,Y1),
	draw_small_rectangle(X1,Y1+Height),
	draw_small_rectangle(X1+Width,Y1+Height),
	set_brush('off').
--
draw_small_rectangle(X0,Y0):-
	X1== X0 - small_rect_width / 2,
	Y1== Y0 - small_rect_height / 2,
	set_brush('on'),
	draw_rectangle(
		X1,
		Y1,
		small_rect_width,
		small_rect_height),
	set_brush('off'),
	draw_rectangle(
		X1,
		Y1,
		small_rect_width,
		small_rect_height).
--
draw_small_circle(X0,Y0):-
	X1== X0 - small_rect_width / 2,
	Y1== Y0 - small_rect_height / 2,
	set_pen({
		color:'Black',
		lineWidth:1}),
	set_brush('Red'),
	draw_ellipse(
		X1,
		Y1,
		small_rect_width,
		small_rect_height),
	set_brush('off'),
	draw_ellipse(
		X1,
		Y1,
		small_rect_width,
		small_rect_height).
--
draw_lines_or_point([p(X1,Y1)]):-!,
	draw_point(X1,Y1).
draw_lines_or_point(List):-
	draw_lines(List).
--
draw_lines([P1|Rest]):-
	P1 == p(X1,Y1),
	Rest == [P2|_],
	P2 == p(X2,Y2),!,
	draw_line(X2,Y2,X1,Y1),
	draw_lines(Rest).
draw_lines(_).
--
set_main_pen:-
	set_pen({
		color:'Navy',
		lineWidth:10,
		endCap:'CAP_ROUND'}).
--
set_preliminary_pen:-
	set_pen({
		color:'LtGray',
		lineWidth:10,
		endCap:'CAP_ROUND'}).
--
set_auxiliary_pen:-
	set_pen({
		color:'Blue',
		lineWidth:1,
		dashArray:[2.0]}).
--
save_image:-
	FileName== con?input_new_file_name(
		"Enter file name please",
		"*.png",
		[
		"*.jpg","JPEG files",
		"*.png","PNG files",
		"*.gif","GIF files",
		"*.bmp","BMP files"
		]),!,
	save(FileName,{
		format:'default',
		compressionQuality:0.95,
		progressiveMode:'on',
		interlacing:'on',
		comment:"The Actor Prolog graphic art"
		}).
save_image.
]
-----------------------------------------------------------------------
class 'DrawerDatabase' (specialized 'Database'):
[
DOMAINS:
--
Target		= object_type(ObjectType);
		  drawing_marker(DrawingMarker);
		  point_list(PointList).
--
PREDICATES:
--
imperative:
--
reset_database;
--
set_object_type(STRING)			- (i);
get_object_type(ObjectType)		- (o);
--
append_point(PointX,PointY)		- (i,i);
replace_recent_point(PointX,PointY)	- (i,i);
clear_point_list;
--
determ:
--
get_previous_point(PointX,PointY)	- (o,o);
get_previous_points(PointList)		- (o);
--
get_previous_points(
	PointX,PointY,
	PointX,PointY)			- (o,o,o,o);
get_previous_points(
	PointX,PointY,
	PointX,PointY,
	PointX,PointY)			- (o,o,o,o,o,o);
get_previous_points(
	PointX,PointY,
	PointX,PointY,
	PointX,PointY,
	PointX,PointY,
	PointX,PointY)			- (o,o,o,o,o,o,o,o,o,o);
--
imperative:
--
remember_drawing_marker(DrawingMarker)	- (i);
forget_drawing_marker;
--
determ:
--
get_drawing_marker(DrawingMarker)	- (o);
--
CLAUSES:
--
reset_database:-
	get_object_type(Type),
	retract_all,
	insert(object_type(Type)).
--
set_object_type(_):-
	retract_all(object_type(_)),
	fail.
set_object_type("Point"):-!,
	insert(object_type('POINT')).
set_object_type("Rectangle"):-!,
	insert(object_type('RECTANGLE')).
set_object_type("RoundRectangle"):-!,
	insert(object_type('ROUNDRECTANGLE2D')).
set_object_type("Ellipse"):-!,
	insert(object_type('ELLIPSE2D')).
set_object_type("Arc"):-!,
	insert(object_type('ARC2D')).
set_object_type("Line"):-!,
	insert(object_type('LINE2D')).
set_object_type("QuadCurve"):-!,
	insert(object_type('QUADCURVE2D')).
set_object_type("CubicCurve"):-!,
	insert(object_type('CUBICCURVE2D')).
set_object_type("Polygon"):-!,
	insert(object_type('POLYGON')).
set_object_type(_).
--
get_object_type(Type):-
	Item== ?match(object_type(_)),
	Item == object_type(Type),!.
get_object_type('RECTANGLE').
--
append_point(X1,Y1):-
	get_previous_point(X2,Y2),
	X1 == X2,
	Y1 == Y2,!.
append_point(X,Y):-
	Item== ?match(point_list(_)),
	Item == point_list(List),!,
	retract_all(Item),
	insert(point_list([p(X,Y)|List])).
append_point(X,Y):-
	insert(point_list([p(X,Y)])).
--
replace_recent_point(X,Y):-
	Item== ?match(point_list(_)),
	Item == point_list([_|List]),!,
	retract_all(Item),
	insert(point_list([p(X,Y)|List])).
replace_recent_point(_,_).
--
clear_point_list:-
	retract_all(point_list(_)).
--
get_previous_point(X1,Y1):-
	Item== ?match(point_list(_)),!,
	Item == point_list([p(X1,Y1)|_]).
--
get_previous_points(List):-
	Item== ?match(point_list(_)),!,
	Item == point_list(List).
get_previous_points(X1,Y1,X2,Y2):-
	Item== ?match(point_list(_)),!,
	Item == point_list([p(X1,Y1),p(X2,Y2)|_]).
get_previous_points(X1,Y1,X2,Y2,X3,Y3):-
	Item== ?match(point_list(_)),!,
	Item == point_list([p(X1,Y1),p(X2,Y2),p(X3,Y3)|_]).
get_previous_points(X1,Y1,X2,Y2,X3,Y3,X4,Y4,X5,Y5):-
	Item== ?match(point_list(_)),!,
	Item == point_list([
			p(X1,Y1),p(X2,Y2),p(X3,Y3),
			p(X4,Y4),p(X5,Y5)|_]).
--
remember_drawing_marker(_):-
	retract_all(drawing_marker(_)),
	fail.
remember_drawing_marker(N):-
	insert(drawing_marker(N)).
--
forget_drawing_marker:-
	retract_all(drawing_marker(_)).
--
get_drawing_marker(N):-
	Item== ?match(drawing_marker(_)),!,
	Item == drawing_marker(N).
]
-----------------------------------------------------------------------
