-----------------------------------------------------------------------
-- An example of Actor Prolog program.                               --
-----------------------------------------------------------------------
-- (c) 2018 IRE RAS Alexei A. Morozov                                --
-----------------------------------------------------------------------
-- Thanks to Andrew Davison                                          --
-- Davison A. Kinect Open Source Programming Secrets (KOPS). -       --
-- McGraw Hill, 2012.                                                --
-----------------------------------------------------------------------
--
import .. from "morozov/Java2D";
import .. from "morozov/Java3D";
import .. from "morozov/Kinect";
--
-----------------------------------------------------------------------
class 'Main' (specialized 'KinectBuffer'):
--
constant:
--
	name	=	"J:/J4K (2018-05-07-Sixth_attempt)"
			"/Kinect2/Video/"
			"006.k2j";
	first_frame	= 1;
	end_frame	= 841;
--
	operating_mode	= 'PLAYING';
	frame_type	= 'POINT_CLOUDS';
	people_index	= 'EXTRACT_PEOPLE';
	skeletons	= 'DETECT_SKELETONS';
--
	displaying_mode	= {
		frame_type,
		people_index,
		skeletons};
--
	color_map	= 'OPTICAL';
	-- color_map	= 'NONE';
--
internal:
--
	window		= ('Canvas3D',
				enable_scene_antialiasing='yes');
	dialog		= ('MainDialog',
				kinect_buffer=self,
				graphics_window=window);
	buffer2D	= ('BufferedImage');
	buffer3D	= ('BufferedScene');
	image		= ('BufferedImage');
	thermal_data	= ('ThermalData');
	state		= ('ProgramState');
	text		= ('Text');
	con		= ('Console');
--
constant:
--
	main_label		= "Skeletons";
	cylinder_radius		= 0.02;
	joint_radius		= 0.03;
	head_radius		= 0.05;
	limb_transparency	= 0.1;
	up_vector		= p(0,1,0);
--
	texture_file_format	= "%08d.jpeg";
--
	texture_directory	=
		"J:/J4K (2018-05-07-Sixth_attempt)"
		"/TEV1/Video/"
		-- "2018-05-07-00-37-47-medical";
		"2018-05-07-00-37-47-purple";
		-- "2018-05-07-00-37-47-gray";
	texture_frame_rate	= 8.845074812967582;
	texture_time_shift	= 2161;
--
	lookup_table_file_name	=
		"J:/J4K (2018-05-07-Sixth_attempt)/stereo-2/"
		"LookUpTable.txt";
--
[
goal:-!,
	dialog ? maximize,
	set_lookup_table(lookup_table_file_name),
	create_list_of_nodes(VisualScene),
	window ? show(VisualScene),
	start.
--
frame_obtained:-
	commit,!,
	get_recent_frame_number(FrameNumber1),
	get_recent_frame_time(Time1),
	state ? set_first_frame_time(FrameNumber1,Time1),
	process_frame(FrameNumber1,Time1).
--
process_frame(N1,Time1):-
	N1 >= first_frame,
	N1 <= end_frame,
	state ? get_first_frame_time(N0,Time0),
	compute_time(N1,Time1,N0,Time0,Time2),
	FileNumber== texture_frame_rate * Time2 / 1000,
	ShortFileName== text?format(
		texture_file_format,?round(FileNumber)),
	ImageToBeLoaded==
		texture_directory + "/" +
		ShortFileName,
	image ? does_exist(ImageToBeLoaded),!,
	image ? load(ImageToBeLoaded),
	-- get_recent_scene(buffer3D,image),
	get_recent_mapping(buffer2D,image),
	get_skeletons(Skeletons),
	SkeletonFigures== ?form_skeletons(Skeletons,buffer2D),
	window ? set_node(
		main_label,
		'BranchGroup'({
			label: main_label,
			allowDetach: 'yes',
			compile: 'yes',
			-- branches: [buffer3D|SkeletonFigures]
			branches: SkeletonFigures
		})).
process_frame(FrameNumber,_):-
	FrameNumber > end_frame,!,
	stop,
	start.
process_frame(_,_).
--
compute_time(N0,_,N0,_,Time):-!,
	Time== 0.0 - texture_time_shift.
compute_time(N1,Time1,N0,Time0,Time3):-
	Time2== N1 * (Time1-Time0) / (N1-N0),
	Time3== Time2 - texture_time_shift.
--
data_transfer_completion:-!,
	stop,
	start.
-----------------------------------------------------------------------
create_list_of_nodes(Nodes):-
	Bounds== 'BoundingSphere'({radius:10.0}),
	create_main_group(MainGroup),
	Nodes == [
		MainGroup,
		'OrbitBehavior'({
			rotationCenter: p(0.0,0.0,-2.1),
			reverseAll: 'yes',
			stopZoom: 'yes',
			minRadius: 1.5,
			schedulingBounds: Bounds
			}),
		'AmbientLight'({
			lightOn: 'yes',
			color: 'White',
			influencingBounds: Bounds
			})
		].
--
create_main_group(MainGroup):-
	MainGroup== 'TransformGroup'({
		allowChildrenRead: 'yes',
		allowChildrenWrite: 'yes',
		branches: [
			'BranchGroup'({
				label: main_label,
				allowDetach: 'yes',
				compile: 'yes',
				branches: []
				})
			]
		}).
-----------------------------------------------------------------------
form_skeletons([Skeleton|Rest1],Buffer2D) = [Figure|Rest2] :-
	Figure== ?form_skeleton(Skeleton,Buffer2D),!,
	Rest2== ?form_skeletons(Rest1,Buffer2D).
form_skeletons([_|Rest1],Buffer2D) = Rest2 :-!,
	Rest2== ?form_skeletons(Rest1,Buffer2D).
form_skeletons(_,_) = [].
-----------------------------------------------------------------------
form_skeleton(Skeleton,Buffer2D) = Figure :-
	Skeleton == {
		head: Head,
		spine: Spine,
		left_arm: LeftArm,
		right_arm: RightArm,
		left_leg: LeftLeg,
		right_leg: RightLeg
		|_},!,
	HeadSpine== ?form_spine(Head,Spine,Buffer2D),
	SpineLeftArm== ?form_arm(Spine,LeftArm,Buffer2D,"Left:"),
	SpineRightArm== ?form_arm(Spine,RightArm,Buffer2D,"Right:"),
	SpineLeftLeg== ?form_leg(Spine,LeftLeg,Buffer2D,"Left:"),
	SpineRightLeg== ?form_leg(Spine,RightLeg,Buffer2D,"Right:"),
	Figure== 'Group'({
		branches: [
			HeadSpine,
			SpineLeftArm,
			SpineRightArm,
			SpineLeftLeg,
			SpineRightLeg
		]
	}).
--
form_spine(Head,Spine,Buffer2D) = Figure :-
	Spine == {
		neck: Neck,
		shoulder: Shoulder,
		mid: Mid,
		base: Base
		|_},!,
	HeadNeck==
		?form_link(Head,Neck,Buffer2D,"Head,Neck"),
	NeckShoulder==
		?form_link(Neck,Shoulder,Buffer2D,"Neck,Shoulder"),
	ShoulderMid==
		?form_link(Shoulder,Mid,Buffer2D,"Shoulder,Mid"),
	MidBase==
		?form_link(Mid,Base,Buffer2D,"Mid,Base"),
	Joint1== ?form_head(Head,Buffer2D),
	Joint2== ?form_joint(Neck,Buffer2D,"Neck"),
	Joint3== ?form_joint(Shoulder,Buffer2D,"Shoulder"),
	Joint4== ?form_joint(Mid,Buffer2D,"Mid"),
	Joint5== ?form_joint(Base,Buffer2D,"Base"),
	Figure== 'Group'({
		branches: [
			HeadNeck,
			NeckShoulder,
			ShoulderMid,
			MidBase,
			Joint1,
			Joint2,
			Joint3,
			Joint4,
			Joint5
		]
	}).
--
form_arm(Spine,Arm,Buffer2D,Side) = Figure:-
	Spine == {
		shoulder: SpineShoulder
		|_},
	Arm == {
		shoulder: ArmShoulder,
		elbow: Elbow,
		wrist: Wrist,
		hand: Hand
		|Rest},!,
	SpineShoulderArmShoulder==
		?form_link(
			SpineShoulder,ArmShoulder,Buffer2D,
			Side+"SpineShoulder,ArmShoulder"),
	ArmShoulderElbow==
		?form_link(
			ArmShoulder,Elbow,Buffer2D,
			Side+"ArmShoulder,Elbow"),
	ElbowWrist==
		?form_link(
			Elbow,Wrist,Buffer2D,
			Side+"Elbow,Wrist"),
	WristHand==
		?form_link(
			Wrist,Hand,Buffer2D,
			Side+"Elbow,Wrist"),
	WristHandRest== ?form_hand(Wrist,Hand,Rest,Buffer2D,Side),
	Joint1== ?form_joint(
			ArmShoulder,Buffer2D,
			Side+"ArmShoulder"),
	Joint2== ?form_joint(
			Elbow,Buffer2D,
			Side+"Elbow"),
	Joint3== ?form_joint(
			Wrist,Buffer2D,
			Side+"Wrist"),
	Figure== 'Group'({
		branches: [
			SpineShoulderArmShoulder,
			ArmShoulderElbow,
			ElbowWrist,
			WristHand,
			WristHandRest,
			Joint1,
			Joint2,
			Joint3
		]
	}).
form_arm(Spine,Arm,Buffer2D,Side) = Figure :-
	Spine == {
		neck: Neck
		|_},
	Arm == {
		shoulder: ArmShoulder,
		elbow: Elbow,
		wrist: Wrist,
		hand: Hand
		|Rest},!,
	NeckArmShoulder==
		?form_link(
			Neck,ArmShoulder,Buffer2D,
			Side+"Neck,ArmShoulder"),
	ArmShoulderElbow==
		?form_link(
			ArmShoulder,Elbow,Buffer2D,
			Side+"ArmShoulder,Elbow"),
	ElbowWrist==
		?form_link(
			Elbow,Wrist,Buffer2D,
			Side+"ArmShoulder,Elbow"),
	WristHand==
		?form_link(
			Wrist,Hand,Buffer2D,
			Side+"Wrist,Hand"),
	Joint1== ?form_joint(
			ArmShoulder,Buffer2D,
			Side+"ArmShoulder"),
	Joint2== ?form_joint(
			Elbow,Buffer2D,
			Side+"Elbow"),
	Joint3== ?form_joint(Wrist,Buffer2D,
			Side+"Wrist"),
	WristHandRest== ?form_hand(Wrist,Hand,Rest,Buffer2D,Side),
	Figure== 'Group'({
		branches: [
			NeckArmShoulder,
			ArmShoulderElbow,
			ElbowWrist,
			WristHand,
			WristHandRest,
			Joint1,
			Joint2,
			Joint3
		]
	}).
--
form_hand(Wrist,Hand,Rest,Buffer2D,Side) = Figure :-
	Rest == {
		tip: Tip,
		thumb: Thumb
		|_},!,
	WristThum== ?form_link(
		Wrist,Thumb,Buffer2D,
		Side+"Wrist,Thumb"),
	HandTip== ?form_link(
		Hand,Tip,Buffer2D,
		Side+"Hand,Tip"),
	Figure== 'Group'({
		branches: [
			WristThum,
			HandTip
		]
	}).
--
form_leg(Spine,Leg,Buffer2D,Side) = Figure :-
	Spine == {
		base: Base
		|_},
	Leg == {
		hip: Hip,
		knee: Knee,
		ankle: Ankle,
		foot: Foot
		|_},!,
	BaseHip== ?form_link(
		Base,Hip,Buffer2D,
		Side+"Base,Hip"),
	HipKnee== ?form_link(
		Hip,Knee,Buffer2D,
		Side+"Hip,Knee"),
	KneeAnkle== ?form_link(
		Knee,Ankle,Buffer2D,
		Side+"Knee,Ankle"),
	AnkleFoot== ?form_link(
		Ankle,Foot,Buffer2D,
		Side+"Ankle,Foot"),
	Joint1== ?form_joint(
		Hip,Buffer2D,
		Side+"Hip"),
	Joint2== ?form_joint(
		Knee,Buffer2D,
		Side+"Knee"),
	Joint3== ?form_joint(
		Ankle,Buffer2D,
		Side+"Ankle"),
	Figure== 'Group'({
		branches: [
			BaseHip,
			HipKnee,
			KneeAnkle,
			AnkleFoot,
			Joint1,
			Joint2,
			Joint3
		]
	}).
--
form_link(Joint1,Joint2,Buffer2D,TextLabel) = Figure :-
	Joint1 == {
		status:S1,
		position:{point:p(J1X,J1Y,J1Z),
		mapping1:M1|_}|_},
	Joint2 == {
		status:S2,
		position:{point:p(J2X,J2Y,J2Z),
		mapping1:M2|_}|_},!,
	select_link_color(
		S1,S2,M1,M2,Buffer2D,TextLabel,
		AppearanceAttributes),
	LengthVector== p(J1X-J2X,J2Y-J1Y,J1Z-J2Z),
	Length== ?length(LengthVector),
	Cylinder== 'Cylinder'({
		radius: cylinder_radius,
		height: Length,
		generateNormals: 'yes',
		xdivisions: 100,
		ydivisions: 1,
		appearance: 'Appearance'(AppearanceAttributes)
		}),
	BaseTG== 'TransformGroup'({
		transform3D: 'Transform3D'({
			translation: [0,Length/2,0]
			}),
		branches: [
			Cylinder
			]
		}),
	AxisAngle== ?calc_rotation(LengthVector),
	Figure== 'TransformGroup'({
		transform3D: 'Transform3D'({
			translation: [-(J1X),J1Y,-(J1Z)],
			rotation: AxisAngle
			}),
		branches: [
			BaseTG
			]
		}).
--
form_joint(Joint,Buffer2D,TextLabel) = Figure :-
	Joint == {
		status:Status,
		position:{point:p(X,Y,Z),mapping1:M|_}|_},!,
	select_joint_color(
		Status,M,Buffer2D,TextLabel,
		MaterialAttributes),
	Figure== 'TransformGroup'({
		transform3D: 'Transform3D'({
			translation: [-(X),Y,-(Z)]
			}),
		branches: [
			'Sphere'({
				radius: joint_radius,
				generateNormals: 'yes',
				divisions: 100,
				appearance: 'Appearance'({
					material: 'Material'(MaterialAttributes)
					})
				})
			]
		}).
--
form_head(Joint,Buffer2D) = Figure :-
	Joint == {
		status:Status,
		position:{point:p(X,Y,Z),mapping1:M1|_}|_},!,
	select_joint_color(
		Status,M1,Buffer2D,"Head",
		MaterialAttributes),
	Figure== 'TransformGroup'({
		transform3D: 'Transform3D'({
			translation: [-(X),Y,-(Z)]
			}),
		branches: [
			'Sphere'({
				radius: head_radius,
				generateNormals: 'yes',
				divisions: 100,
				appearance: 'Appearance'({
					material: 'Material'(MaterialAttributes)
					})
				})
			]
		}).
--
select_link_color(
		'TRACKED','TRACKED',
		p(X1,Y1),p(X2,Y2),
		Buffer2D,
		TextLabel,
		AppearanceAttributes):-!,
	DeltaX== X2 - X1,
	DeltaY== Y2 - Y1,
	Distance== ?max(?abs(DeltaX),?abs(DeltaY)) + 1,
	StepX== DeltaX / Distance,
	StepY== DeltaY / Distance,
	average_color(
		1,Distance,
		X1,Y1,
		StepX,StepY,
		Buffer2D,
		0,0,0,
		0,
		R1,G1,B1),
	thermal_data ? average(TextLabel,R1,G1,B1,R2,G2,B2),
	Color== color3(R2/255,G2/255,B2/255),
	AppearanceAttributes == {
		material: 'Material'({
			ambientColor: Color,
			emissiveColor: Color
			})
		}.
select_link_color(_,_,_,_,_,_,AppearanceAttributes):-
	AppearanceAttributes == {
		material: 'Material'({}),
		transparencyAttributes: 'TransparencyAttributes'({
			transparency: limb_transparency,
			transparencyMode: 'FASTEST'
			})
		}.
--
average_color(	Position,Distance,
		X,Y,
		StepX,StepY,
		Buffer2D,
		R1,G1,B1,
		N,
		R2,G2,B2):-
	Position <= Distance,
	Buffer2D ? get_pixel(?round(X),?round(Y),Ri,Gi,Bi),!,
	average_color(
		Position+1,Distance,
		X+StepX,Y+StepY,
		StepX,StepY,
		Buffer2D,R1+Ri,G1+Gi,B1+Bi,N+1,R2,G2,B2).
average_color(_,_,_,_,_,_,_,R1,G1,B1,N,R2,G2,B2):-
	N > 0,!,
	R2== ?round(R1 / N),
	G2== ?round(G1 / N),
	B2== ?round(B1 / N).
average_color(_,_,_,_,_,_,_,_,_,_,_,0,0,0).
--
select_joint_color('TRACKED',p(X,Y),Buffer2D,TextLabel,Attributes):-
	Buffer2D ? get_pixel(X,Y,R1,G1,B1),!,
	thermal_data ? average(TextLabel,R1,G1,B1,R2,G2,B2),
	Color== color3(R2/255,G2/255,B2/255),
	Attributes== {
		ambientColor: Color,
		emissiveColor: Color
		}.
select_joint_color('INFERRED',_,_,_,Attributes):-!,
	Color == 'Black',
	Attributes== {
		ambientColor: Color,
		emissiveColor: Color,
		diffuseColor: Color,
		specularColor: Color
		}.
select_joint_color(_,_,_,_,{}).
-----------------------------------------------------------------------
calc_rotation(LengthVector) = [X,Y,Z,AxisAngle] :-
	NormalizedVector== ?normalize(LengthVector),
	AxisVector== ?cross(up_vector,NormalizedVector),
	AxisVector == p(X,Y,Z),!,
	AxisAngle== ?angle(up_vector,LengthVector).
calc_rotation(_) = [].
--
normalize(Vector) = ?scale(Vector,Norm) :-!,
	Norm== 1.0 / ?length(Vector).
normalize(_) = _ :-
	break('CannotNormalize').
--
cross(p(X1,Y1,Z1),p(X2,Y2,Z2)) = p(X3,Y3,Z3) :-!,
	X3== Y1*Z2 - Z1*Y2,
	Y3== X2*Z1 - Z2*X1,
	Z3== X1*Y2 - Y1*X2.
cross(_,_) = _ :-
	break('CannotCross').
--
angle(V1,V2) = V3 :-
	VDot== ?dot(V1,V2) / (?length(V1)*?length(V2)),
	V3== ?angle(VDot).
--
angle(VDot) = Angle :-
	VDot < -1.0,!,
	Angle== ?arccos(-1.0).
angle(VDot) = Angle :-
	VDot > 1.0,!,
	Angle== ?arccos(1.0).
angle(VDot) = Angle :-
	Angle== ?arccos(VDot).
--
dot(p(X1,Y1,Z1),p(X2,Y2,Z2)) = X1*X2 + Y1*Y2 + Z1*Z2.
--
scale(p(X,Y,Z),N) = p(X*N,Y*N,Z*N).
--
length(p(X,Y,Z)) = Length :-!,
	Length== ?sqrt(?power(X,2)+?power(Y,2)+?power(Z,2)).
length(_) = 0.
]
-----------------------------------------------------------------------
class 'MainDialog' (specialized 'Dialog'):
--
internal:
--
	kinect_buffer;
	graphics_window;
--
constant:
--
	is_top_level_window	= 'yes';
--
[
action("play"):-!,
	kinect_buffer ? start.
action("pause"):-!,
	kinect_buffer ? pause.
]
-----------------------------------------------------------------------
class 'ProgramState' (specialized 'Database'):
[
set_first_frame_time(_,_):-
	find(_),!.
set_first_frame_time(Number,Time):-
	insert(f(Number,Time)).
--
get_first_frame_time(Number,Time):-
	Item== ?match(_),!,
	Item == f(Number,Time).
get_first_frame_time(0,0).
]
-----------------------------------------------------------------------
class 'ThermalData' (specialized 'HashMap'):
[
average(Label,R1,G1,B1,R4,G4,B4):-
	Value== ?get(Label),!,
	Value == value(N1,R2,G2,B2),
	N2== N1 + 1,
	R3== R1 + R2,
	G3== G1 + G2,
	B3== B1 + B2,
	put(Label,value(N2,R3,G3,B3)),
	R4== ?round(R3 / N2),
	G4== ?round(G3 / N2),
	B4== ?round(B3 / N2).
average(Label,R,G,B,R,G,B):-
	put(Label,value(1,R,G,B)).
]
-----------------------------------------------------------------------
