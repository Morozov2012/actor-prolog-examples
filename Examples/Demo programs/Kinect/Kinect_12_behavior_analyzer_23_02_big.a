-----------------------------------------------------------------------
-- An example of Actor Prolog program.                               --
-- (c) 2017 IRE RAS Alexei A. Morozov                                --
-----------------------------------------------------------------------
import .. from "morozov/Java2D";
import .. from "morozov/Kinect";
import .. from "morozov/ColorMap";
-----------------------------------------------------------------------
project: (('VideoPlayer'))
-----------------------------------------------------------------------
class 'VideoPlayer' (specialized 'Dialog'):
--
internal:
--
buffer			= ('ControllableBuffer',
				main_window,
				legend_window);
main_window		= ('Canvas2D',
				background_color='SystemControl');
legend_window		= ('Canvas2D',
				background_color='SystemControl');
con			= ('Console');
text_operations		= ('Text');
file_operations		= ('File');
color_maps		= ('ColorMap');
--
constant:
--
is_top_level_window	= 'yes';
exit_on_close		= 'yes';
--
[
goal:-!,
	-- maximize,
	show.
--
action("Select"):-
	FileName== con?input_file_name(
		"Please select data file",
		"*.k2j",
		[
		"*.k2j","K2J Files",
		"*.*","All Files"
		]),!,
	buffer ? set_name(FileName),
	put('title',"2.5D Video Player ["+FileName+"]").
action("Play"):-
	check_file_name,!,
	update_displaying_mode,
	update_color_map,
	update_people_coloring,
	buffer ? set_operating_mode('PLAYING'),
	buffer ? start.
action("Export"):-
	check_file_name,
	Directory== con?input_directory_name(
		"Please select output directory",
		""),!,
	file_operations ? make_directory(Directory),
	update_displaying_mode,
	update_color_map,
	update_people_coloring,
	buffer ? set_output_directory(Directory),
	buffer ? set_operating_mode('READING'),
	buffer ? start.
action("Pause"):-!,
	buffer ? pause.
action("Stop"):-!,
	buffer ? stop.
--
check_file_name:-
	buffer ? does_exist(),!.
check_file_name:-
	FileName== buffer?get_name(),
	con ? error(
		"Illegal File name",
		text_operations?format(
			"The '%s' file does not exist.",FileName)),
	fail.
--
modified_control(1010):-!,
	update_displaying_mode.
modified_control(1020):-!,
	update_displaying_mode.
modified_control(1030):-!,
	update_displaying_mode.
modified_control(1040):-!,
	update_displaying_mode.
modified_control(1050):-!,
	update_color_map.
modified_control(1060):-!,
	update_people_coloring.
--
update_displaying_mode:-
	TextFT== ?cast("STRING",?get(1010)),
	TextPIM== ?cast("STRING",?get(1020)),
	TextCM== ?cast("STRING",?get(1030)),
	TextSM== ?cast("STRING",?get(1040)),
	FrameType== ?string_to_frame_type(TextFT),
	PeopleIndexMode== ?string_to_people_index(TextPIM),
	CircumscriptionMode== ?string_to_circumscription(TextCM),
	SkeletonsMode== ?string_to_skeletons(TextSM),!,
	DisplayingMode== {
		frame_type: FrameType,
		people_index: PeopleIndexMode,
		circumscription: [CircumscriptionMode],
		skeletons: SkeletonsMode
		},
	buffer ? set_displaying_mode(DisplayingMode).
update_displaying_mode.
--
update_color_map:-
	TextCM== ?cast("STRING",?get(1050)),
	ColorMap== color_maps?string_to_color_map_name(TextCM),
	buffer ? set_color_map(ColorMap).
--
update_people_coloring:-
	TextPC== ?cast("STRING",?get(1060)),
	PeopleColors== ?string_to_people_colors(TextPC),!,
	buffer ? set_people_colors(PeopleColors).
update_people_coloring.
--
string_to_frame_type("Depth Maps")= 'DEPTH_MAPS'.
string_to_frame_type("Colored Depth Maps")= 'COLORED_DEPTH_MAPS'.
string_to_frame_type("Infrared")= 'INFRARED'.
string_to_frame_type(
	"Long Exposure Infrared")= 'LONG_EXPOSURE_INFRARED'.
string_to_frame_type("Mapped Color")= 'MAPPED_COLOR'.
string_to_frame_type("Point Clouds")= 'POINT_CLOUDS'.
string_to_frame_type("Color")= 'COLOR'.
string_to_frame_type("Device Tuning")= 'DEVICE_TUNING'.
string_to_frame_type("None")= 'NONE'.
--
string_to_people_index("Tincture People")= 'TINCTURE_PEOPLE'.
string_to_people_index("Paint People")= 'PAINT_PEOPLE'.
string_to_people_index("Extract People")= 'EXTRACT_PEOPLE'.
string_to_people_index(
	"Adaptively Extract People")= 'ADAPTIVELY_EXTRACT_PEOPLE'.
string_to_people_index("Project People")= 'PROJECT_PEOPLE'.
string_to_people_index("None")= 'NONE'.
--
string_to_circumscription("Total Rectangles")= 'TOTAL_RECTANGLES'.
string_to_circumscription(
	"Skeleton Rectangles")= 'SKELETON_RECTANGLES'.
string_to_circumscription(
	"Total Parallelepipeds")= 'TOTAL_PARALLELEPIPEDS'.
string_to_circumscription(
	"Skeleton Parallelepipeds")= 'SKELETON_PARALLELEPIPEDS'.
string_to_circumscription("None")= 'NONE'.
--
string_to_skeletons("Detect Skeletons")= 'DETECT_SKELETONS'.
string_to_skeletons("Track Skeletons")= 'DETECT_AND_TRACK_SKELETONS'.
string_to_skeletons("None")= 'NONE'.
--
string_to_people_colors("Rainbow")
	= ['Red','Green','Blue','Yellow','Magenta','Cyan'].
string_to_people_colors("Red")= ['Red'].
string_to_people_colors("Orange")= ['Orange'].
string_to_people_colors("Yellow")= ['Yellow'].
string_to_people_colors("Green")= ['Green'].
string_to_people_colors("Cyan")= ['Cyan'].
string_to_people_colors("Blue")= ['Blue'].
string_to_people_colors("Magenta")= ['Magenta'].
]
-----------------------------------------------------------------------
class 'ControllableBuffer' (specialized 'KinectBuffer'):
--
constant:
--
	name;
--
	operating_mode		= 'PLAYING';
--
internal:
--
	main_window;
	legend_window;
--
	analyzer		= ('BehaviorAnalyzer',
					buffer=self,
					graphics_window=main_window);
--
	image			= ('BufferedImage');
	directory_name		= ('DirectoryName');
	con			= ('Console');
	text			= ('Text');
	timer_operations	= ('Timer');
--
constant:
--
	maximal_chronicle_length	= 1.5;
	slow_motion_coefficient		= -1;
--
constant:
--
	ellipse_radius		= 10;
--
	velocity_bound_1	= 0.5;
	velocity_bound_2	= 1.0;
	velocity_bound_3	= 2.0;
	velocity_bound_4	= 3.0;
	velocity_bound_5	= 4.0;
	velocity_bound_6	= 5.0;
	velocity_bound_7	= 6.0;
	velocity_bound_8	= 7.0;
--
	velocity_color_1	= 'Violet';
	velocity_color_2	= 'Magenta';
	velocity_color_3	= 'Cyan';
	velocity_color_4	= 'Emerald';
	velocity_color_5	= 'Lime';
	velocity_color_6	= 'Yellow';
	velocity_color_7	= 'Orange';
	velocity_color_8	= 'Pink';
	velocity_color_9	= 'Red';
--
[
frame_obtained:-
	commit,
	get_recent_frame_number(FrameNumber),
	get_recent_frame_time(FrameTime),
	get_recent_frame_rate(FrameRate),
	get_recent_image(image),
	DisplayingMode== ?get_displaying_mode(),
	DisplayingMode == {
		frame_type: FrameType,
		skeletons: SkeletonsMode|_},!,
	get_image_size_in_pixels(IW,IH,FrameType),
	main_window ? refine_width(IW/IH),
	main_window ? suspend_redrawing,
	main_window ? clear,
	main_window ? draw_image(image,0,0,1,1),
	report_time(FrameNumber,FrameTime,FrameRate),
	draw_tracks_or_skeletons(SkeletonsMode,FrameType,IW,IH),
	main_window ? draw_now,
	save_frame_if_necessary(?get_operating_mode(),FrameNumber).
--
data_transfer_error(ErrorName):-!,
	con ? error("Data Transfer Error",ErrorName).
--
get_image_size_in_pixels(IW,IH,'COLOR'):-!,
	get_color_image_size_in_pixels(IW,IH).
get_image_size_in_pixels(IW,IH,_):-
	get_depth_image_size_in_pixels(IW,IH).
-----------------------------------------------------------------------
draw_tracks_or_skeletons('DETECT_SKELETONS',FrameType,IW,IH):-!,
	hide_legend,
	get_skeletons(Skeletons),
	draw_skeletons(IW,IH,Skeletons,FrameType).
draw_tracks_or_skeletons(
		'DETECT_AND_TRACK_SKELETONS',FrameType,IW,IH):-!,
	draw_legend,
	get_tracks(Tracks),
	draw_tracks(IW,IH,Tracks,FrameType),
	get_chronicle(Chronicle),
	analyzer ? analyse_chronicle(Chronicle).
draw_tracks_or_skeletons(_,_,_,_):-
	hide_legend.
-----------------------------------------------------------------------
draw_skeletons(IW,IH,[Skeleton|Rest],FrameType):-!,
	draw_skeleton(IW,IH,Skeleton,FrameType),
	draw_parallelepipeds(IW,IH,Skeleton,FrameType),
	draw_rectangles(IW,IH,Skeleton,FrameType),
	draw_skeletons(IW,IH,Rest,FrameType).
draw_skeletons(_,_,_,_).
-----------------------------------------------------------------------
draw_tracks(IW,IH,[Track|Rest],FrameType):-
	Track == {is_complete:'no',skeletons:Skeletons|_},!,
	draw_recent_skeleton(IW,IH,Skeletons,{},FrameType),
	draw_tracks(IW,IH,Rest,FrameType).
draw_tracks(IW,IH,[_|Rest],FrameType):-!,
	draw_tracks(IW,IH,Rest,FrameType).
draw_tracks(_,_,_,_).
-----------------------------------------------------------------------
draw_recent_skeleton(IW,IH,[S2],_,FrameType):-!,
	draw_skeleton(IW,IH,S2,FrameType),
	-- draw_identifier(IW,IH,S2,FrameType),
	-- draw_hand_trajectories(IW,IH,S1,S2,FrameType),
	draw_parallelepipeds(IW,IH,S2,FrameType),
	draw_rectangles(IW,IH,S2,FrameType).
draw_recent_skeleton(IW,IH,[S2|Rest],_,FrameType):-!,
	-- draw_hand_trajectories(IW,IH,S1,S2,FrameType),
	draw_recent_skeleton(IW,IH,Rest,S2,FrameType).
draw_recent_skeleton(_,_,_,_,_).
--
draw_skeleton(IW,IH,Skeleton,FrameType):-
	Skeleton == {
		head: Head,
		spine: Spine,
		left_arm: LeftArm,
		right_arm: RightArm,
		left_leg: LeftLeg,
		right_leg: RightLeg
		|_},
	draw_spine(IW,IH,Head,Spine,FrameType),
	draw_arm(IW,IH,Spine,LeftArm,FrameType),
	draw_arm(IW,IH,Spine,RightArm,FrameType),
	draw_leg(IW,IH,Spine,LeftLeg,FrameType),
	draw_leg(IW,IH,Spine,RightLeg,FrameType),
	fail.
draw_skeleton(_,_,_,_).
--
draw_spine(IW,IH,Head,Spine,FrameType):-
	Spine == {
		neck: Neck,
		shoulder: Shoulder,
		mid: Mid,
		base: Base
		|_},!,
	draw_link(IW,IH,Head,Neck,FrameType),
	draw_link(IW,IH,Neck,Shoulder,FrameType),
	draw_link(IW,IH,Shoulder,Mid,FrameType),
	draw_link(IW,IH,Mid,Base,FrameType).
draw_spine(_,_,_,_,_).
--
draw_arm(IW,IH,Spine,Arm,FrameType):-
	Spine == {shoulder:SpineShoulder|_},
	Arm == {
		shoulder: ArmShoulder,
		elbow: Elbow,
		wrist: Wrist,
		hand: Hand
		|Rest},!,
	draw_link(IW,IH,SpineShoulder,ArmShoulder,FrameType),
	draw_link(IW,IH,ArmShoulder,Elbow,FrameType),
	draw_link(IW,IH,Elbow,Wrist,FrameType),
	draw_link(IW,IH,Wrist,Hand,FrameType),
	draw_hand(IW,IH,Wrist,Hand,Rest,FrameType).
draw_arm(IW,IH,Spine,Arm,FrameType):-
	Spine == {neck:Neck|_},
	Arm == {
		shoulder: ArmShoulder,
		elbow: Elbow,
		wrist: Wrist,
		hand: Hand
		|Rest},!,
	draw_link(IW,IH,Neck,ArmShoulder,FrameType),
	draw_link(IW,IH,ArmShoulder,Elbow,FrameType),
	draw_link(IW,IH,Elbow,Wrist,FrameType),
	draw_link(IW,IH,Wrist,Hand,FrameType),
	draw_hand(IW,IH,Wrist,Hand,Rest,FrameType).
draw_arm(_,_,_,_,_).
--
draw_hand(IW,IH,Wrist,Hand,Rest,FrameType):-
	Rest == {tip:Tip,thumb:Thumb|_},!,
	draw_link(IW,IH,Wrist,Thumb,FrameType),
	draw_link(IW,IH,Hand,Tip,FrameType).
draw_hand(_,_,_,_,_,_).
--
draw_leg(IW,IH,Spine,Leg,FrameType):-
	Spine == {base:Base|_},
	Leg == {
		hip: Hip,
		knee: Knee,
		ankle: Ankle,
		foot: Foot
		|_},!,
	draw_link(IW,IH,Base,Hip,FrameType),
	draw_link(IW,IH,Hip,Knee,FrameType),
	draw_link(IW,IH,Knee,Ankle,FrameType),
	draw_link(IW,IH,Ankle,Foot,FrameType).
draw_leg(_,_,_,_,_).
--
draw_link(IW,IH,Joint1,Joint2,FrameType):-
	FrameType == 'COLOR',
	Joint1 == {
		status:Status1,
		position:{mapping2:p(Joint1X1,Joint1Y1)|_}|_},
	Joint2 == {
		status:Status2,
		position:{mapping2:p(Joint2X1,Joint2Y1)|_}|_},!,
	select_line_width(Status1,Status2,LineWidth),
	main_window ? set_pen(
		{color:'DkGreen',lineWidth:LineWidth}),
	Joint1X2== Joint1X1 / IW,
	Joint1Y2== Joint1Y1 / IH,
	Joint2X2== Joint2X1 / IW,
	Joint2Y2== Joint2Y1 / IH,
	main_window ? draw_line(
		Joint1X2,Joint1Y2,Joint2X2,Joint2Y2),
	draw_joint(IW,IH,Joint1,FrameType),
	draw_joint(IW,IH,Joint2,FrameType).
draw_link(IW,IH,Joint1,Joint2,_):-
	Joint1 == {
		status:Status1,
		position:{mapping1:p(Joint1X1,Joint1Y1)|_}|_},
	Joint2 == {
		status:Status2,
		position:{mapping1:p(Joint2X1,Joint2Y1)|_}|_},!,
	select_line_width(Status1,Status2,LineWidth),
	main_window ? set_pen(
		{color:'DkGreen',lineWidth:LineWidth}),
	Joint1X2== Joint1X1 / IW,
	Joint1Y2== Joint1Y1 / IH,
	Joint2X2== Joint2X1 / IW,
	Joint2Y2== Joint2Y1 / IH,
	main_window ? draw_line(
		Joint1X2,Joint1Y2,Joint2X2,Joint2Y2).
	-- draw_joint(IW,IH,Joint1,FrameType),
	-- draw_joint(IW,IH,Joint2,FrameType).
draw_link(_,_,_,_,_).
--
select_line_width('TRACKED','TRACKED',3):-!.
select_line_width(_,_,1).
--
select_line_width('TRACKED','TRACKED','TRACKED',3):-!.
select_line_width(_,_,_,1).
--
draw_joint(IW,IH,Joint,'COLOR'):-
	Joint == {position:{mapping2:p(X1,Y1)|_}|_},!,
	main_window ? set_brush('Orange'),
	main_window ? set_font({size:21,weight:'WEIGHT_BOLD'}),
	main_window ? set_text_alignment('CENTER','CENTER'),
	X2== (X1 - ellipse_radius) / IW,
	Y2== (Y1 - ellipse_radius) / IH,
	W2== ellipse_radius*2 / IW,
	H2== ellipse_radius*2 / IH,
	main_window ? draw_ellipse(X2,Y2,W2,H2).
draw_joint(IW,IH,Joint,_):-
	Joint == {position:{mapping1:p(X1,Y1)|_}|_},!,
	main_window ? set_brush('Orange'),
	main_window ? set_font({size:21,weight:'WEIGHT_BOLD'}),
	main_window ? set_text_alignment('CENTER','CENTER'),
	X2== (X1 - ellipse_radius) / IW,
	Y2== (Y1 - ellipse_radius) / IH,
	W2== ellipse_radius*2 / IW,
	H2== ellipse_radius*2 / IH,
	main_window ? draw_ellipse(X2,Y2,W2,H2).
draw_joint(_,_,_,_).
-----------------------------------------------------------------------
draw_identifier(IW,IH,Skeleton,'COLOR'):-
	Skeleton == {
		identifier: Id,
		head: Head
		|_},
	Head == {position:{mapping2:p(Joint1X1,Joint1Y1)|_}|_},!,
	Joint1X2== Joint1X1 / IW,
	Joint1Y2== Joint1Y1 / IH,
	main_window ? set_brush('Yellow'),
	main_window ? set_font({size:32,weight:'WEIGHT_BOLD'}),
	main_window ? set_text_alignment('CENTER','BOTTOM'),
	main_window ? draw_text(
		Joint1X2,
		Joint1Y2-0.10,
		text?format("%d",Id+1)).
draw_identifier(IW,IH,Skeleton,_):-
	Skeleton == {
		identifier: Id,
		head: Head
		|_},
	Head == {position:{mapping1:p(Joint1X1,Joint1Y1)|_}|_},!,
	Joint1X2== Joint1X1 / IW,
	Joint1Y2== Joint1Y1 / IH,
	main_window ? set_brush('Yellow'),
	main_window ? set_font({size:32,weight:'WEIGHT_BOLD'}),
	main_window ? set_text_alignment('CENTER','BOTTOM'),
	main_window ? draw_text(
		Joint1X2,
		Joint1Y2-0.10,
		text?format("%d",Id+1)).
draw_identifier(_,_,_,_).
-----------------------------------------------------------------------
draw_hand_trajectories(IW,IH,S1,S2,FrameType):-
	S1 == {
		left_arm:{hand:LeftHand1|_},
		right_arm:{hand:RightHand1|_}
		|_},
	S2 == {
		left_arm:{hand:LeftHand2|_},
		right_arm:{hand:RightHand2|_}
		|_},!,
	draw_trajectory_segment(
		IW,IH,
		LeftHand1,
		LeftHand2,
		FrameType),
	draw_trajectory_segment(
		IW,IH,
		RightHand1,
		RightHand2,
		FrameType).
draw_hand_trajectories(_,_,_,_,_).
--
draw_trajectory_segment(IW,IH,J1,J2,FrameType):-
	FrameType == 'COLOR',
	J1 == {
		status:STATUS1,
		position:{
			mapping2:p(Joint1X1,Joint1Y1)
			|_}
		|_},
	J2 == {
		status:STATUS2,
		position:{
			mapping2:p(Joint2X1,Joint2Y1),
			velocity:p(Vx2,Vy2,Vz2)|_}
		|_},!,
	V2== ?velocity(Vx2,Vy2,Vz2),
	select_trajectory_color(V2,Color),
	select_line_width(STATUS1,STATUS2,W),
	main_window ? set_pen({color:Color,lineWidth:W}),
	Joint1X2== Joint1X1 / IW,
	Joint1Y2== Joint1Y1 / IH,
	Joint2X2== Joint2X1 / IW,
	Joint2Y2== Joint2Y1 / IH,
	main_window ? draw_line(
		Joint1X2,Joint1Y2,Joint2X2,Joint2Y2).
draw_trajectory_segment(IW,IH,J1,J2,_):-
	J1 == {
		status:Status1,
		position:{
			mapping1:p(Joint1X1,Joint1Y1)
			|_}
		|_},
	J2 == {
		status:Status2,
		position:{
			mapping1:p(Joint2X1,Joint2Y1),
			velocity:p(Vx2,Vy2,Vz2)|_}
		|_},!,
	V2== ?velocity(Vx2,Vy2,Vz2),
	select_trajectory_color(V2,Color),
	select_line_width(Status1,Status2,W),
	main_window ? set_pen({color:Color,lineWidth:W}),
	Joint1X2== Joint1X1 / IW,
	Joint1Y2== Joint1Y1 / IH,
	Joint2X2== Joint2X1 / IW,
	Joint2Y2== Joint2Y1 / IH,
	main_window ? draw_line(
		Joint1X2,Joint1Y2,Joint2X2,Joint2Y2).
draw_trajectory_segment(_,_,_,_,_).
--
velocity(Vx,Vy,Vz) = V :-
	V== ?sqrt(?power(Vx,2)+?power(Vy,2)+?power(Vz,2)).
--
select_trajectory_color(Velocity,velocity_color_1):-
	Velocity <= velocity_bound_1,!.
select_trajectory_color(Velocity,velocity_color_2):-
	Velocity > velocity_bound_1,
	Velocity <= velocity_bound_2,!.
select_trajectory_color(Velocity,velocity_color_3):-
	Velocity > velocity_bound_2,
	Velocity <= velocity_bound_3,!.
select_trajectory_color(Velocity,velocity_color_4):-
	Velocity > velocity_bound_3,
	Velocity <= velocity_bound_4,!.
select_trajectory_color(Velocity,velocity_color_5):-
	Velocity > velocity_bound_4,
	Velocity <= velocity_bound_5,!.
select_trajectory_color(Velocity,velocity_color_6):-
	Velocity > velocity_bound_5,
	Velocity <= velocity_bound_6,!.
select_trajectory_color(Velocity,velocity_color_7):-
	Velocity > velocity_bound_6,
	Velocity <= velocity_bound_7,!.
select_trajectory_color(Velocity,velocity_color_8):-
	Velocity > velocity_bound_7,
	Velocity <= velocity_bound_8,!.
select_trajectory_color(_,velocity_color_9).
-----------------------------------------------------------------------
draw_parallelepipeds(IW,IH,Skeleton,FrameType):-
	Skeleton == {total_parallelepiped:Parallelepiped|_},
	draw_parallelepiped(
		IW,IH,Parallelepiped,FrameType,'Blue',3),
	fail.
draw_parallelepipeds(IW,IH,Skeleton,FrameType):-
	Skeleton == {skeleton_parallelepiped:Parallelepiped|_},
	draw_parallelepiped(
		IW,IH,Parallelepiped,FrameType,'Violet',3),
	fail.
draw_parallelepipeds(_,_,_,_).
--
draw_parallelepiped(IW,IH,Parallelepiped,FrameType,Color,LineWidth):-
	Parallelepiped == {
		vertex11: V11,
		vertex12: V12,
		vertex13: V13,
		vertex14: V14,
		vertex21: V21,
		vertex22: V22,
		vertex23: V23,
		vertex24: V24
		|_},
	draw_line(IW,IH,V11,V12,FrameType,Color,LineWidth,{}),
	draw_line(IW,IH,V12,V13,FrameType,Color,LineWidth,{}),
	draw_line(IW,IH,V13,V14,FrameType,Color,LineWidth,{}),
	draw_line(IW,IH,V14,V11,FrameType,Color,LineWidth,{}),
	DashArray== {dashArray:[10,10]},
	draw_line(IW,IH,V21,V22,FrameType,Color,LineWidth,DashArray),
	draw_line(IW,IH,V22,V23,FrameType,Color,LineWidth,DashArray),
	draw_line(IW,IH,V23,V24,FrameType,Color,LineWidth,DashArray),
	draw_line(IW,IH,V24,V21,FrameType,Color,LineWidth,DashArray),
	draw_line(IW,IH,V11,V21,FrameType,Color,LineWidth,DashArray),
	draw_line(IW,IH,V12,V22,FrameType,Color,LineWidth,DashArray),
	draw_line(IW,IH,V13,V23,FrameType,Color,LineWidth,DashArray),
	draw_line(IW,IH,V14,V24,FrameType,Color,LineWidth,DashArray),
	fail.
draw_parallelepiped(_,_,_,_,_,_).
-----------------------------------------------------------------------
draw_rectangles(IW,IH,Skeleton,FrameType):-
	Skeleton == {total_rectangle:TotalRectangle|_},
	draw_rectangle(IW,IH,TotalRectangle,FrameType,'Orange',5),
	fail.
draw_rectangles(IW,IH,Skeleton,FrameType):-
	Skeleton == {skeleton_rectangle:SkeletonRectangle|_},
	draw_rectangle(IW,IH,SkeletonRectangle,FrameType,'Red',5),
	fail.
draw_rectangles(_,_,_,_).
--
draw_rectangle(IW,IH,Rectangle,FrameType,Color,LineWidth):-
	Rectangle == {
		vertex1: V1,
		vertex2: V2,
		vertex3: V3,
		vertex4: V4
		|_},
	draw_line(IW,IH,V1,V2,FrameType,Color,LineWidth,{}),
	draw_line(IW,IH,V2,V3,FrameType,Color,LineWidth,{}),
	draw_line(IW,IH,V3,V4,FrameType,Color,LineWidth,{}),
	draw_line(IW,IH,V4,V1,FrameType,Color,LineWidth,{}),
	fail.
draw_rectangle(_,_,_,_,_,_).
-----------------------------------------------------------------------
draw_line(IW,IH,Vertex1,Vertex2,FrameType,Color,LineWidth,DashArray):-
	FrameType == 'COLOR',
	Vertex1 == {mapping2:p(Vertex1X1,Vertex1Y1)|_},
	Vertex2 == {mapping2:p(Vertex2X1,Vertex2Y1)|_},
	main_window ? set_pen(
		{color:Color,lineWidth:LineWidth|DashArray}),!,
	Vertex1X2== Vertex1X1 / IW,
	Vertex1Y2== Vertex1Y1 / IH,
	Vertex2X2== Vertex2X1 / IW,
	Vertex2Y2== Vertex2Y1 / IH,
	main_window ? draw_line(
		Vertex1X2,Vertex1Y2,Vertex2X2,Vertex2Y2).
draw_line(IW,IH,Vertex1,Vertex2,_,Color,LineWidth,DashArray):-
	Vertex1 == {mapping1:p(Vertex1X1,Vertex1Y1)|_},
	Vertex2 == {mapping1:p(Vertex2X1,Vertex2Y1)|_},
	main_window ? set_pen(
		{color:Color,lineWidth:LineWidth|DashArray}),!,
	Vertex1X2== Vertex1X1 / IW,
	Vertex1Y2== Vertex1Y1 / IH,
	Vertex2X2== Vertex2X1 / IW,
	Vertex2Y2== Vertex2Y1 / IH,
	main_window ? draw_line(
		Vertex1X2,Vertex1Y2,Vertex2X2,Vertex2Y2).
draw_line(_,_,_,_,_,_,_,_).
-----------------------------------------------------------------------
report_time(_,_,_):-
	main_window ? set_brush('Green'),
	main_window ? set_font({size:36}),
	fail.
report_time(FrameNumber,FrameTime,FrameRate):-
	FrameNumber >= 0,
	Time== timer_operations?convert_to_time(FrameTime),
	Time == time(Hours,Minutes,Seconds,Milliseconds),!,
	main_window ? set_text_alignment('LEFT','BOTTOM'),
	main_window ? draw_text(
		0.02, 0.98,
		text?format(
			"Time: %02d:%02d:%02d.%03d Frame: %d",
			Hours,Minutes,Seconds,Milliseconds,
			FrameNumber)),
	main_window ? set_text_alignment('RIGHT','BOTTOM'),
	main_window ? draw_text(
		0.95, 0.98,
		text?format(
			"Frame Rate: %2.1f",FrameRate)).
report_time(_,_,_).
--
draw_legend:-
	legend_window ? suspend_redrawing,
	legend_window ? clear,
	legend_window ? set_brush('off'),
	legend_window ? set_font({size:12,weight:'WEIGHT_BOLD'}),
	legend_window ? set_text_alignment('LEFT','CENTER'),
	X0r== 0.05,
	Y0r== 0.05,
	X1r== X0r + 0.10,
	Y1r== Y0r + 0.03,
	Y2r== Y1r + 0.03,
	Y3r== Y2r + 0.03,
	Y4r== Y3r + 0.03,
	Y5r== Y4r + 0.03,
	Y6r== Y5r + 0.03,
	Y7r== Y6r + 0.03,
	Y8r== Y7r + 0.03,
	Y9r== Y8r + 0.03,
	T1== text?format("Velocity <= %1.2f",velocity_bound_1),
	T2== text?format("Velocity > %1.2f",velocity_bound_1),
	T3== text?format("Velocity > %1.2f",velocity_bound_2),
	T4== text?format("Velocity > %1.2f",velocity_bound_3),
	T5== text?format("Velocity > %1.2f",velocity_bound_4),
	T6== text?format("Velocity > %1.2f",velocity_bound_5),
	T7== text?format("Velocity > %1.2f",velocity_bound_6),
	T8== text?format("Velocity > %1.2f",velocity_bound_7),
	T9== text?format("Velocity > %1.2f",velocity_bound_8),
	legend_window ? set_pen({color:velocity_color_1}),
	legend_window ? draw_text(X1r,Y1r,T1),
	legend_window ? set_pen({color:velocity_color_2}),
	legend_window ? draw_text(X1r,Y2r,T2),
	legend_window ? set_pen({color:velocity_color_3}),
	legend_window ? draw_text(X1r,Y3r,T3),
	legend_window ? set_pen({color:velocity_color_4}),
	legend_window ? draw_text(X1r,Y4r,T4),
	legend_window ? set_pen({color:velocity_color_5}),
	legend_window ? draw_text(X1r,Y5r,T5),
	legend_window ? set_pen({color:velocity_color_6}),
	legend_window ? draw_text(X1r,Y6r,T6),
	legend_window ? set_pen({color:velocity_color_7}),
	legend_window ? draw_text(X1r,Y7r,T7),
	legend_window ? set_pen({color:velocity_color_8}),
	legend_window ? draw_text(X1r,Y8r,T8),
	legend_window ? set_pen({color:velocity_color_9}),
	legend_window ? draw_text(X1r,Y9r,T9),
	legend_window ? draw_now.
--
hide_legend:-
	legend_window ? clear.
-----------------------------------------------------------------------
set_output_directory(Directory):-
	directory_name ? retract_all,
	directory_name ? insert(Directory).
--
save_frame_if_necessary('READING',FrameNumber):-
	directory_name ? find(Directory),!,
	FileName== text?format("%s/%08d.png",Directory,FrameNumber),
	main_window ? save(FileName,{
		format:'default',
		compressionQuality:0.95,
		progressiveMode:'on',
		interlacing:'on',
		comment:"The Actor Prolog project"
		}),
	continue_exporting_if_necessary.
save_frame_if_necessary(_,_).
--
continue_exporting_if_necessary:-
	eof,!.
continue_exporting_if_necessary:-
	start.
]
-----------------------------------------------------------------------
class 'BehaviorAnalyzer' (specialized 'Alpha'):
--
internal:
--
buffer;
graphics_window;
--
con			= ('Console');
--
[
analyse_chronicle([Frame|Past]):-
	Frame == {skeletons:Skeletons|_},
	analyse_skeletons(Skeletons,Past),!.
analyse_chronicle([_|Rest]):-!,
	analyse_chronicle(Rest).
analyse_chronicle(_).
--
analyse_skeletons(Skeletons,Past):-
	a_moving_arm_of_a_person(Skeletons,Id,Side,Point,V,T),
	analyse_moving_arm(Skeletons,Past,Id,Side,Point,V,T),!.
--
analyse_moving_arm(Skeletons,Past,Id,Side,Point,V,T):-
	is_near_a_head_of_a_person(Skeletons,Past,Id,Side,Point,V,T),!,
	graphics_window ? set_font({size:64,weight:'WEIGHT_BOLD'}),
	graphics_window ? set_pen({color:'Red'}),
	graphics_window ? set_text_alignment('CENTER','CENTER'),
	graphics_window ? draw_text(
		0.5,0.1,
		"A punch in the face is detected!").
analyse_moving_arm(_,_,_,_,_,_,_):-!,
	fail.
analyse_moving_arm(Skeletons,Past,Id,Side,Point,_,T):-
	is_near_a_hand_of_a_person(Skeletons,Past,Id,Side,Point,T),!,
	graphics_window ? set_font({size:64,weight:'WEIGHT_BOLD'}),
	graphics_window ? set_pen({color:'Cyan'}),
	graphics_window ? set_text_alignment('CENTER','CENTER'),
	graphics_window ? draw_text(
		0.5,0.1,
		"A handshake is detected!").
-----------------------------------------------------------------------
a_moving_arm_of_a_person([S|_],Id,Side,Point,V,T):-
	a_moving_hand_of_a_person(S,Id,Side,Point,V,T).
a_moving_arm_of_a_person([_|Rest],Id,Side,Point,V,T):-!,
	a_moving_arm_of_a_person(Rest,Id,Side,Point,V,T).
--
a_moving_hand_of_a_person(Skeleton,Id,'Right',Point,V,T):-
	Skeleton == {	identifier:Id,
			right_arm:{tip:RightHand|_},
			time:T|_},
	a_moving_joint(RightHand,Point,V).
a_moving_hand_of_a_person(Skeleton,Id,'Left',Point,V,T):-
	Skeleton == {	identifier:Id,
			left_arm:{tip:RightHand|_},
			time:T|_},
	a_moving_joint(RightHand,Point,V).
--
a_moving_joint(Joint,Point,V2):-
	Joint == {
		status:'TRACKED',
		position:{point:Point,velocity:p(Vx,Vy,Vz)|_}
		|_},
	V2== ?velocity(Vx,Vy,Vz),
	V2 >= 0.1.
-----------------------------------------------------------------------
is_near_a_head_of_a_person(Skeletons,Past,Id1,Side1,P1,V1,T1):-
	select_the_head_of_a_person(Skeletons,Id1,P1,V1,Id2),
	check_the_head_of_the_person(Past,Id1,Side1,Id2,T1,T2),
	Duration== ?duration(T2,T1),
	Duration > 0.01,!.
is_near_a_head_of_a_person([_|Rest],Past,Id,Side,Point,V,T):-
	is_near_a_head_of_a_person(Rest,Past,Id,Side,Point,V,T).
--
select_the_head_of_a_person([Skeleton|_],Id1,Point1,Velocity1,Id2):-
	Skeleton == {	identifier:Id2,
			head:{	status:'TRACKED',
				position:{point:Point2|_}|_}
			|_},
	Id1 <> Id2,
	is_fast_and_near(Point1,Velocity1,Point2),!.
select_the_head_of_a_person([_|Rest],Id1,Point1,Velocity1,Id2):-
	select_the_head_of_a_person(Rest,Id1,Point1,Velocity1,Id2).
--
check_the_head_of_the_person([Frame|Past],Id1,Side1,Id2,_,T2):-
	Frame == {time:T1,skeletons:Skeletons|_},
	get_hand(Id1,Side1,Skeletons,Point1,Velocity1),
	get_head(Id2,Skeletons,Point2),
	is_fast_and_near(Point1,Velocity1,Point2),!,
	check_the_head_of_the_person(Past,Id1,Side1,Id2,T1,T2).
check_the_head_of_the_person(_,_,_,_,Time,Time).
--
is_fast_and_near(p(X1,Y1,Z1),Velocity1,p(X2,Y2,Z2)):-
	Delta== ?distance(X1-X2,Y1-Y2,Z1-Z2),
	MD== 1 - ?fuzzy_metrics(Delta,0.30,0.05),
	MV== ?fuzzy_metrics(Velocity1,4.0,1.0),
	MD * MV >= 0.25.
-----------------------------------------------------------------------
is_near_a_hand_of_a_person(Skeletons,Past,Id1,Side1,P1,T1):-
	select_a_hand_of_a_person(Skeletons,Id1,P1,Id2,Side2),
	check_the_hand_of_the_person(Past,Id1,Side1,Id2,Side2,T1,T2),
	Duration== ?duration(T2,T1),
	Duration > 0.30,!.
is_near_a_hand_of_a_person([_|Rest],Past,Id,Side,Point,T):-
	is_near_a_hand_of_a_person(Rest,Past,Id,Side,Point,T).
--
select_a_hand_of_a_person([Skeleton|_],Id1,Point1,Id2,'Right'):-
	Skeleton == {	identifier:Id2,
			right_arm:{
				tip:{	status:'TRACKED',
					position:{point:Point2|_}|_}
				|_}
			|_},
	Id1 <> Id2,
	is_near(Point1,Point2),!.
select_a_hand_of_a_person([Skeleton|_],Id1,Point1,Id2,'Left'):-
	Skeleton == {	identifier:Id2,
			left_arm:{
				tip:{	status:'TRACKED',
					position:{point:Point2|_}|_}
				|_}
			|_},
	Id1 <> Id2,
	is_near(Point1,Point2),!.
select_a_hand_of_a_person([_|Rest],Id1,Point1,Id2,Side2):-
	select_a_hand_of_a_person(Rest,Id1,Point1,Id2,Side2).
--
check_the_hand_of_the_person([Frame|Past],Id1,Side1,Id2,Side2,_,T2):-
	Frame == {time:T1,skeletons:Skeletons|_},
	get_hand(Id1,Side1,Skeletons,Point1,_),
	get_hand(Id2,Side2,Skeletons,Point2,_),
	is_near(Point1,Point2),!,
	check_the_hand_of_the_person(Past,Id1,Side1,Id2,Side2,T1,T2).
check_the_hand_of_the_person(_,_,_,_,_,Time,Time).
--
is_near(p(X1,Y1,Z1),p(X2,Y2,Z2)):-
	Delta== ?distance(X1-X2,Y1-Y2,Z1-Z2),
	Delta < 0.10.
-----------------------------------------------------------------------
get_hand(Id,Side,[Skeleton|_],Point,Velocity):-
	Skeleton == {identifier:Id|_},!,
	get_hand(Skeleton,Side,Point,Velocity).
get_hand(Id,Side,[_|Rest],Point,Velocity):-
	get_hand(Id,Side,Rest,Point,Velocity).
--
get_hand(Skeleton,'Right',Point,Velocity):-!,
	Skeleton == {	right_arm:{
				tip:{	status:'TRACKED',
					position:{
						point:Point,
						velocity:p(Vx,Vy,Vz)|_}
					|_}
				|_}
			|_},
	Velocity== ?velocity(Vx,Vy,Vz).
get_hand(Skeleton,'Left',Point,Velocity):-
	Skeleton == {	left_arm:{
				tip:{	status:'TRACKED',
					position:{
						point:Point,
						velocity:p(Vx,Vy,Vz)|_}
					|_}
				|_}
			|_},
	Velocity== ?velocity(Vx,Vy,Vz).
--
get_head(Id,[Skeleton|_],Point):-
	Skeleton == {identifier:Id|_},!,
	Skeleton == {head:{
			status:'TRACKED',
			position:{point:Point|_}|_}
			|_}.
get_head(Id,[_|Rest],Point):-
	get_head(Id,Rest,Point).
--
distance(Dx,Dy,Dz) = ?sqrt(?power(Dx,2)+?power(Dy,2)+?power(Dz,2)).
--
velocity(Vx,Vy,Vz) = V :-
	V== ?sqrt(?power(Vx,2)+?power(Vy,2)+?power(Vz,2)).
--
duration(T1,T2) = (T2 - T1) / 1000.
--
fuzzy_metrics(X,T,H) = 1.0 :-
     X >= T + H,!.
fuzzy_metrics(X,T,H) = 0.0 :-
     X <= T - H,!.
fuzzy_metrics(X,T,H) = V :-
     V== (X-T+H) * (1 / (2*H)).
]
-----------------------------------------------------------------------
class 'DirectoryName' (specialized 'Database'):
[
]
-----------------------------------------------------------------------
